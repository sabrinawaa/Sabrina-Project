 !system,"[ ! -e sps ] && [ -d /afs/cern.ch/eng/acc-models/sps/2021 ] && ln -nfs /afs/cern.ch/eng/acc-models/sps/2021 sps";
 !system,"[ ! -e sps ] && git clone https://gitlab.cern.ch/acc-models/acc-models-sps -b 2021 sps";

option, -echo;

call,file="sps1.seq";
call,file="sps/strengths/ft_q26.str";

beam,particle=proton, PC=14;

use,sequence=sps;

!twiss;
LOE.22002, K3=14;
LOE.32002, K3=14;
LOEN.52002, K3=14;

call,file="sps/toolkit/macro.madx";
qx=26.713; ! originally 26.62
qy=26.58;

exec, sps_match_tunes(qx,qy);

! to set the chromaticity using knob in LSA 
exec,sps_define_sext_knobs;
exec,sps_set_chroma_weights_ft; 

! the qph and qpv correspond to normalized chromaticity
qph_setvalue = +0.1;
qpv_setvalue = +0.0;





! to match the chromaticity (varying the chroma knob settings)
sps_match_chroma(dq1_target_value,dq2_target_value) : macro = {
 match, sequence=sps;
        vary, name=qph_setvalue,  step=1.e-8;
        vary, name=qpv_setvalue,  step=1.e-8;
        global, dq1=dq1_targetvalue, dq2=dq2_targetvalue;
        jacobian, calls=10, tolerance=1e-25;
 endmatch;
};

dq1_targetvalue=3.12;
dq2_targetvalue=2;
exec, sps_match_chroma(dq1_targetvalue,dq2_targetvalue);

!select,flag=twiss,column=name,keyword,s,betx,alfx,mux,bety,alfy,muy,x,px,y,py,t,pt,dx,dpx,dy,dpy,k2l,k3l;
!twiss,file="sps.tfs";

seqedit,sequence=sps;
cycle,start=KIK.11654;
endedit;

sps4: sequence, refer = entry, L = 27646.0152;
sps, at=0.0;
sps, at=6911.5038;
sps, at=13823.0076;
sps, at=20734.5114;
endsequence;

use,sequence=sps4;

ptc_create_universe; 
ptc_create_layout,model=1,method=6,nst=10,exact;

select,flag=ptc_twiss,column=name,keyword,s,betx,alfx,mux,bety,alfy,muy,x,px,y,py,t,pt,dx,dpx,dy,dpy,k2l,k3l;

ptc_twiss,closed_orbit,file="ptc_twiss.tfs", table=ptc_twiss, summary_file="ptc_twiss_summ.tfs",x=-0.003,px=0.0005,no=4,icase=56; 

ptc_end;

